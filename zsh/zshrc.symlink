# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dev/dotfiles/

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev/

# Setup the plateform
case "`uname`" in
    "Linux")
        export PLATFORM="linux"
        ;;
    "Darwin")
        export PLATFORM="macos"
        stty -ixon # Deactivate special handling of ctrl-s / ctrl-q
        ;;
    *)
        >&2 echo "Unknown platform: `uname`"
        export PLATFORN="unknown"
        ;;
esac

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
    source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
    source $file
done

unset config_files

source ${ZSH}/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source ${ZSH}/zsh/zsh-nvm/zsh-nvm.plugin.zsh

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# because we are civilized people
set -o vi

# http://unix.stackexchange.com/questions/44115/how-do-i-perform-a-reverse-history-search-in-zshs-vi-mode
bindkey "^R" history-incremental-search-backward

bindkey -M vicmd "/" vi-history-search-backward
bindkey -M vicmd "?" vi-history-search-forward
bindkey -M vicmd "n" vi-repeat-search
bindkey -M vicmd "N" vi-rev-repeat-search

# http://dougblack.io/words/zsh-vi-mode.html
export KEYTIMEOUT=1

set blink-matching-paren on

# poupou's inline vim editor
autoload edit-command-line; zle -N edit-command-line
bindkey -M vicmd v edit-command-line

# mkvenv, workon, etc for python2/3 mess
export VIRTUALENVWRAPPER_PYTHON=`which python3`
source /usr/local/bin/virtualenvwrapper.sh

export PATH="$HOME/.yarn/bin:$PATH"

# Jenv Setup (java env switcher)
# ==============================

export PATH="$HOME/.jenv/bin:$PATH"
export PATH="/Users/laurent/.jenv/shims:${PATH}"
source "${HOME}/.jenv/completions/jenv.sh"

jenv rehash 2>/dev/null
export JENV_LOADED=1
unset JAVA_HOME
jenv() {
    typeset command
    command="$1"
    if [ "$#" -gt 0 ]; then
        shift
    fi

    case "$command" in
        enable-plugin|rehash|shell|shell-options)
            eval `jenv "sh-$command" "$@"`;;
        *)
            command jenv "$command" "$@";;
    esac
}

# /Jenv

export PATH="/usr/local/opt/llvm/bin:$PATH"
